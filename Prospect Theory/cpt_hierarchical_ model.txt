# Fits Cumulative Prospect Theory
# Data from Rieskamp (30 participants, 60 gamble-pairs 
# with positive outcomes, 60 gamble-pairs with negative
# outcomes, and 60 gamble-pairs with mixed outcomes)


model
{


# The model assumes that every participant has a unique parameter-value 
# on each of the six parameters of hierarchical-CPT (see, Nilsson, Rieskamp, & Wagnemakers, 2010).
# Furthermore, it assumes that the individual values on parameter-dimension X 
# are sampled from the hyperdistribution for X. This has to be defined in the model code.

	
	for (j in 1:30)
	{

		# We start by defining that participant j has a unique parameter-value 
		# on each of the six parameters: alpha, beta, gamma, delta, lambda, and luce 
		# (the choice-rule parameter).

		alpha[j] <- phi(alpha.phi[j])
		beta[j] <- phi(beta.phi[j])
		gamma[j] <- phi(gamma.phi[j])
		delta[j] <- phi(delta.phi[j])	
		luce[j]   <- exp(lluce[j]) 
            	lambda[j] <- exp(llambda[j])
		
		# We put group-level normal´s on the individual parameters:

		alpha.phi[j] ~ dnorm(mu.phi.alpha,tau.phi.alpha)I(-3, 3)
		beta.phi[j] ~ dnorm(mu.phi.beta,tau.phi.beta)I(-3, 3)
		gamma.phi[j] ~ dnorm(mu.phi.gamma,tau.phi.gamma)I(-3, 3)
		delta.phi[j] ~ dnorm(mu.phi.delta,tau.phi.delta)I(-3, 3)
            	lluce[j]   ~ dnorm(lmu.luce, ltau.luce)
            	llambda[j] ~ dnorm(lmu.lambda, ltau.lambda)


		
	}


	# Here priors for the hyperdistributions are defined:
	mu.phi.alpha ~ dnorm(0,1)
	tau.phi.alpha <- pow(sigma.phi.alpha,-2)
	sigma.phi.alpha ~ dunif(0,10)

	mu.phi.beta ~ dnorm(0,1)
	tau.phi.beta <- pow(sigma.phi.beta,-2)
	sigma.phi.beta ~ dunif(0,10)

	mu.phi.gamma ~ dnorm(0,1)
	tau.phi.gamma <- pow(sigma.phi.gamma,-2)
	sigma.phi.gamma ~ dunif(0,10)

	mu.phi.delta ~ dnorm(0,1)
	tau.phi.delta <- pow(sigma.phi.delta,-2)
	sigma.phi.delta ~ dunif(0,10)
	
	lmu.lambda    ~ dunif(-2.3, 1.61) 
      	ltau.lambda  <- pow(lsigma.lambda,-2)
      	lsigma.lambda ~ dunif(0,1.13)    

      	lmu.luce    ~ dunif(-2.3, 1.61)   
      	ltau.luce  <- pow(lsigma.luce,-2)
      	lsigma.luce ~ dunif(0,1.13)       

	
	# To obtain the mean of the hyper distribution on the wanted scale:
	mu.alpha <- phi(mu.phi.alpha) 
	mu.beta <- phi(mu.phi.beta)
	mu.gamma <- phi(mu.phi.gamma) 
	mu.delta <- phi(mu.phi.delta) 
	mu.lambda <- exp(lmu.lambda)
      	mu.luce   <- exp(lmu.luce)




# It is now time to define how the model should be fit to data. 

	for (j in 1:30)# Subject-loop
	{
	
   		for (i in 1:60)# Item-Loop, positive gambles,gamble A
   		{

# j:s subjective value of outcome 1 in gamble A of gamble-pair i, v.x.a[i,j], equals:

     			v.x.a[i,j] <- pow(prospects.a[i,1],alpha[j])

# where prospects.a[i,1] = outcome 1 and alpha[j] = j:s value on the alpha parameter
# j:s subjective value of outcome 2 in gamble A
       
      			v.y.a[i,j] <- pow(prospects.a[i,3],alpha[j])       

# j:s subjective probability of outcomes 1, w.x.a[i,j], and 2, w.y.a[i,j].

     			w.x.a[i,j] <- pow(prospects.a[i,2],gamma[j]) / pow(z.a[i,j],(1/gamma[j])) 
      			w.y.a[i,j] <- pow(prospects.a[i,4],gamma[j]) / pow(z.a[i,j],(1/gamma[j])) 
      			z.a[i,j]   <- pow(prospects.a[i,2],gamma[j]) + pow(prospects.a[i,4],gamma[j]) 

# j:s subjective value of gamble A in gamble-pair i

      			Vf.a[i,j]  <- w.x.a[i,j] * v.x.a[i,j] + w.y.a[i,j] * v.y.a[i,j]
      
   		}


   		for (i in 1:60)# Item-Loop, positive gambles,gamble B
   		{
      			v.x.b[i,j] <- pow(prospects.b[i,1],alpha[j])       
      			v.y.b[i,j] <- pow(prospects.b[i,3],alpha[j])       

      			w.x.b[i,j] <- pow(prospects.b[i,2],gamma[j]) / pow(z.b[i,j],(1/gamma[j])) 
      			w.y.b[i,j] <- pow(prospects.b[i,4],gamma[j]) / pow(z.b[i,j],(1/gamma[j])) 
      			z.b[i,j]   <- pow(prospects.b[i,2],gamma[j]) + pow(prospects.b[i,4],gamma[j]) 

      			Vf.b[i,j]  <- w.x.b[i,j] * v.x.b[i,j] + w.y.b[i,j] * v.y.b[i,j]
      
   		}
 
   		for (i in 1:60)# Item-Loop, positive gambles,choice-rule
   		{

# It is defined that j:s choice for gamble-pair i is sampled from a Bernoulli-distribution 
# with p = binval[i,j].
# The exponential choice-rule is used to compute binval[i,j] 
# (in this case: binval[i,j] = probability of B-choice).
      
			binval[i,j] <- (1)/(1+exp((-1*luce[j])*(Vf.b[i,j]-Vf.a[i,j])))
			rawdata[i,j] ~ dbern(binval[i,j])
   		}


		
   		for (i in 61:120)# Item-Loop, negative gambles,gamble A
   		{
     			v.x.a[i,j] <- (-1) * pow((-1 * prospects.a[i,1]),beta[j])       
      			v.y.a[i,j] <- (-1) * pow((-1 * prospects.a[i,3]),beta[j])       

     			w.x.a[i,j] <- pow(prospects.a[i,2],delta[j]) / pow(z.a[i,j],(1/delta[j])) 
      			w.y.a[i,j] <- pow(prospects.a[i,4],delta[j]) / pow(z.a[i,j],(1/delta[j])) 
      			z.a[i,j]   <- pow(prospects.a[i,2],delta[j]) + pow(prospects.a[i,4],delta[j]) 

      			Vf.a[i,j]  <- w.x.a[i,j] * v.x.a[i,j] + w.y.a[i,j] * v.y.a[i,j]
      
   		}

   		for (i in 61:120)# Item-Loop, negative gambles,gamble B
   		{
      			v.x.b[i,j] <- (-1) * pow((-1 * prospects.b[i,1]),beta[j])       
      			v.y.b[i,j] <- (-1) * pow((-1 * prospects.b[i,3]),beta[j])       

      			w.x.b[i,j] <- pow(prospects.b[i,2],delta[j]) / pow(z.b[i,j],(1/delta[j])) 
      			w.y.b[i,j] <- pow(prospects.b[i,4],delta[j]) / pow(z.b[i,j],(1/delta[j])) 
      			z.b[i,j]   <- pow(prospects.b[i,2],delta[j]) + pow(prospects.b[i,4],delta[j]) 

      			Vf.b[i,j]  <- w.x.b[i,j] * v.x.b[i,j] + w.y.b[i,j] * v.y.b[i,j]
      
   		}
 
   		for (i in 61:120)# Item-Loop, negative gambles,choice-rule
   		{
      
			binval[i,j] <- (1)/(1+exp((-1*luce[j])*(Vf.b[i,j]-Vf.a[i,j])))
			rawdata[i,j] ~ dbern(binval[i,j])
   		}


		for (i in 121:180)# Item-Loop, mixed gambles,gamble A
   		{
     			v.x.a[i,j] <- pow(prospects.a[i,1],alpha[j])       
      			v.y.a[i,j] <- (-1 * lambda[j]) * pow((-1 * prospects.a[i,3]),beta[j])       

     			w.x.a[i,j] <- pow(prospects.a[i,2],gamma[j]) / pow(z.a[i,j],(1/gamma[j])) 
      			w.y.a[i,j] <- pow(prospects.a[i,4],delta[j]) / pow(z.a[i,j],(1/delta[j])) 
      			z.a[i,j]   <- pow(prospects.a[i,2],gamma[j]) + pow(prospects.a[i,4],delta[j]) 

      			Vf.a[i,j]  <- w.x.a[i,j] * v.x.a[i,j] + w.y.a[i,j] * v.y.a[i,j]
      
   		}

   		for (i in 121:180)# Item-Loop, mixed gambles,gamble B
   		{
      			v.x.b[i,j] <- pow(prospects.b[i,1],alpha[j])       
      			v.y.b[i,j] <- (-1 * lambda[j]) * pow((-1 * prospects.b[i,3]),beta[j])       

      			w.x.b[i,j] <- pow(prospects.b[i,2],gamma[j]) / pow(z.b[i,j],(1/gamma[j])) 
      			w.y.b[i,j] <- pow(prospects.b[i,4],delta[j]) / pow(z.b[i,j],(1/delta[j])) 
      			z.b[i,j]   <- pow(prospects.b[i,2],gamma[j]) + pow(prospects.b[i,4],delta[j]) 

      			Vf.b[i,j]  <- w.x.b[i,j] * v.x.b[i,j] + w.y.b[i,j] * v.y.b[i,j]
      
   		}
 
   		for (i in 121:180)# Item-Loop, mixed gambles,choice-rule
   		{
      
			binval[i,j] <- (1)/(1+exp((-1*luce[j])*(Vf.b[i,j]-Vf.a[i,j])))
			rawdata[i,j] ~ dbern(binval[i,j])
   		}


 
 	}   
}